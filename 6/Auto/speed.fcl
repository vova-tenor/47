// Block definition (there may be more than one block per file)
FUNCTION_BLOCK speed


// Define input variables
VAR_INPUT
    speed			     : REAL;
	temperatureOnStreet	 : REAL;
	requestedSpeed       : REAL;
	masaAuto 			 : REAL;
END_VAR

// Define output variable
VAR_OUTPUT
    toSpeed : REAL;
	toSpeedUp : REAL;
END_VAR


// Fuzzify input variable 'speed'
FUZZIFY speed
    TERM slow 			:= (0, 1) (50, 0);
    TERM comfortable 	:= (45, 0) (60, 1) (90, 1) (115, 0);
    TERM quick 			:= (110, 0) (150, 1);
END_FUZZIFY

// Fuzzify input variable 'temperatureOnStreet'
FUZZIFY temperatureOnStreet
    TERM cold 		 := (0, 1) (15, 0) ;
    TERM normal := (10, 0) (15, 1) (20, 1) (25, 0);
    TERM hot		 := (20, 0) (50, 1);
END_FUZZIFY

// Fuzzify input variable 'requestedSpeed'
FUZZIFY requestedSpeed
    TERM slow 			:= (0, 1) (40, 0);
    TERM comfortable 	:= (35, 0) (45, 1) (90, 1) (95, 0);
    TERM quick 			:= (90, 0) (110, 1);
END_FUZZIFY

// Fuzzify input variable 'masaAuto' im kg
FUZZIFY masaAuto
    TERM small 		:= (0, 1) (1.5, 1) (1.8, 1) (2.05, 0);
	TERM average 	:= (2, 0) (2.5, 1) (3.05, 0);
    TERM big 		:= (3, 0) (5, 1);
END_FUZZIFY


// Defzzzify output variable 'toSpeed'
DEFUZZIFY toSpeed
    TERM slower := (0, 0) (5, 1) (10, 0);
    TERM comfortable 	:= (10, 0) (15, 1) (18, 0);
    TERM faster 	:= (18, 0) (25, 1) (30, 0);

    METHOD : COG;	// Use 'Center Of Gravity' defuzzification method

    DEFAULT := 0;	// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

// Defzzzify output variable 'toSpeedUp'
DEFUZZIFY toSpeedUp
    TERM little := (0, 0) (5, 1) (10, 0);
    TERM normal	:= (10, 0) (15, 1) (18, 0);
    TERM big 	:= (18, 0) (25, 1) (30, 0);

    METHOD : COG;	// Use 'Center Of Gravity' defuzzification method

    DEFAULT := 0;	// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY


RULEBLOCK No1
	AND : MIN;	// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)

    ACT : MIN;	// Use 'min' activation method

    ACCU : MAX;	// Use 'max' accumulation method

    RULE 1 : IF speed IS slow AND requestedSpeed IS comfortable AND temperatureOnStreet IS normal
             THEN toSpeed IS comfortable;

    RULE 2 : IF speed IS slow AND requestedSpeed IS quick AND temperatureOnStreet IS normal
             THEN toSpeed IS faster;

	RULE 3 : IF speed IS quick AND requestedSpeed IS comfortable AND temperatureOnStreet IS cold
             THEN toSpeed IS slower;

	RULE 4 : IF speed IS slow AND requestedSpeed IS quick  AND  masaAuto IS small
             THEN toSpeedUp IS big;

    RULE 5 : IF speed IS quick  AND requestedSpeed IS comfortable AND masaAuto IS average
             THEN toSpeedUp IS little;

	RULE 6 : IF speed IS slow AND requestedSpeed IS comfortable AND masaAuto IS big
             THEN toSpeedUp IS little;

END_RULEBLOCK


END_FUNCTION_BLOCK
